Execute (Buffer: Construct Default):
  let g:buffer = dapper#view#Buffer#new()
  call g:buffer.open()
Then:
  AssertEqual 'hide',   &bufhidden
  AssertEqual 0,        &buflisted
  AssertEqual 'nofile', &buftype
  AssertEqual 0,        &swapfile

Execute (Buffer: Set Buffer Variable):
  let g:buffer = dapper#view#Buffer#new()
  call g:buffer.setbufvar('test_var', 1)
  call g:buffer.open()
Then:
  AssertEqual 1, b:test_var

Execute (Buffer: Get Buffer Variable):
  AssertEqual 'nofile', g:buffer.getbufvar('&buftype')
  AssertEqual 1, g:buffer.getbufvar('test_var')

" Since split moves the cursor to the new split, check relative position by
" moving *back* to the original window and checking that it *isn't* the owned
" buffer.
Execute (Buffer: Open Horizontal Split):
  buffer 1
  call g:buffer.split('', 'botright')
Then:
  wincmd k
  Assert !exists('b:test_var')

Execute (Buffer: Open Vertical Split):
  buffer 1
  call g:buffer.vsplit('', 'botright')
Then:
  wincmd h
  Assert !exists('b:test_var')

Execute (Buffer: Wipeout Buffer):
  let g:bufnr = g:buffer.bufnr()
  AssertEqual g:bufnr, g:buffer.setBuffer(bufnr('%'), 'bwipeout')

Given (Ten Lines of Text):
  first line
  second line
  third line
  fourth line
  fifth line
  sixth line
  seventh line
  eighth line
  ninth line
  tenth line

Execute (Buffer: Delete Buffer Contents):
  call g:buffer.setBuffer(bufnr('%'))
  call g:buffer.deleteLines(0, 5)
Expect:
  sixth line
  seventh line
  eighth line
  ninth line
  tenth line

Execute (Buffer: Delete Everything):
  call g:buffer.setBuffer(bufnr('%'))
  call g:buffer.deleteLines(0, -1)
Expect:
  

Execute (Buffer: Replace Buffer Contents):
  call g:buffer.setBuffer(bufnr('%'))
  let g:lines = [
    \ 'foo1',
    \ 'foo2',
    \ 'foo3',
  \ ]
  call g:buffer.replaceLines(0, 5, g:lines)
Expect:
  foo1
  foo2
  foo3
  sixth line
  seventh line
  eighth line
  ninth line
  tenth line

Execute (Buffer: Replace Last Lines):
  call g:buffer.setBuffer(bufnr('%'))
  let g:lines = [
    \ 'foo1',
    \ 'foo2',
    \ 'foo3',
  \ ]
  call g:buffer.replaceLines(6, -1, g:lines)
Expect:
  first line
  second line
  third line
  fourth line
  fifth line
  sixth line
  foo1
  foo2
  foo3

Execute (Buffer: Replace Everything):
  call g:buffer.setBuffer(bufnr('%'))
  let g:lines = [
    \ 'foo1',
    \ 'foo2',
    \ 'foo3',
  \ ]
  call g:buffer.replaceLines(0, -1, g:lines)
Expect:
  foo1
  foo2
  foo3

Execute (Buffer: Insert Lines):
  call g:buffer.setBuffer(bufnr('%'))
  let g:lines = [
    \ 'foo1',
    \ 'foo2',
    \ 'foo3',
  \ ]
  call g:buffer.insertLines(0, g:lines)
Expect:
  foo1
  foo2
  foo3
  first line
  second line
  third line
  fourth line
  fifth line
  sixth line
  seventh line
  eighth line
  ninth line
  tenth line

Execute (Buffer: Insert Lines At End):
  call g:buffer.setBuffer(bufnr('%'))
  let g:lines = [
    \ 'foo1',
    \ 'foo2',
    \ 'foo3',
  \ ]
  call g:buffer.insertLines(-1, g:lines)
Expect:
  first line
  second line
  third line
  fourth line
  fifth line
  sixth line
  seventh line
  eighth line
  ninth line
  tenth line
  foo1
  foo2
  foo3

Execute (Buffer: Is Open in this Tab, Yes):
  tabnew
  call g:buffer.setBuffer(bufnr('%'))
Then:
  AssertEqual v:true, g:buffer.isOpenInTab()
  AssertEqual v:true, g:buffer.isOpenInTab(tabpagenr())

Execute (Buffer: Is Open in Previous Tab):
  call g:buffer.setBuffer(bufnr('%'))
  let g:old_tab = tabpagenr()
  tabnew
Then:
  AssertEqual v:true, g:buffer.isOpenInTab(g:old_tab)
  AssertEqual v:false, g:buffer.isOpenInTab()
  AssertEqual v:false, g:buffer.isOpenInTab(tabpagenr())

Execute (Buffer: Is Open in Previous Tab, and In Current):
  call g:buffer.open()
Then:
  AssertEqual v:true, g:buffer.isOpenInTab(g:old_tab)
  AssertEqual v:true, g:buffer.isOpenInTab()
  AssertEqual v:true, g:buffer.isOpenInTab(tabpagenr())

Execute (Buffer: Is Not Open At All):
  tabnew
  call g:buffer.setBuffer(bufnr('%'))
  tabnew
  call g:buffer.destroy()
Then:
  AssertEqual v:false, g:buffer.isOpenInTab(g:old_tab)
  AssertEqual v:false, g:buffer.isOpenInTab()
  AssertEqual v:false, g:buffer.isOpenInTab(tabpagenr())

Execute (Buffer: Switch, Not Open):
  AssertThrows call g:buffer.switch()
  AssertNotEqual -1, match(g:vader_exception, 'ERROR(NotFound)')

Execute (Buffer: Switch, Is Open, Same Tab):
  call g:buffer.setBuffer(bufnr('%'))
  tabnew
  let g:tabnr = tabpagenr()
  call g:buffer.open()
  call g:buffer.switch()
Then:
  AssertEqual g:tabnr, tabpagenr()
  AssertEqual g:buffer.bufnr(), bufnr('%')

Execute (Buffer: Switch, Is Open, Different Tab (but not allowed)):
  tabnew
  call g:buffer.setBuffer(bufnr('%'))
  let g:tabnr = tabpagenr()
  tabnew
Then:
  AssertThrows call g:buffer.switch(v:false)
  AssertNotEqual -1, match(g:vader_exception, 'ERROR(NotFound)')

Execute (Buffer: Switch, Is Open, Different Tab (which is allowed)):
  call g:buffer.switch()
Then:
  AssertEqual g:tabnr, tabpagenr()
  AssertEqual g:buffer.bufnr(), bufnr('%')

Execute (Buffer: Switch, Is Only Open in Different (Specific) Tab (which isn't allowed)):
  tabnew
Then:
  AssertThrows call g:buffer.switch(v:false)
  AssertNotEqual -1, match(g:vader_exception, 'ERROR(NotFound)')

Execute (Buffer: Switch, Is Only Open in Different (Specific) Tab (which is given)):
  tabnew
  call g:buffer.setBuffer(bufnr('%'))
  let g:tabnr = tabpagenr()
  tabnew
  call g:buffer.switch(v:false, g:tabnr)
Then:
  AssertEqual g:tabnr, tabpagenr()
  AssertEqual g:buffer.bufnr(), bufnr('%')

Execute (Buffer: Switch, Is Open in Different (Specific) Tab (which is given), and Current (not given)):
  tabnew
  call g:buffer.setBuffer(bufnr('%'))
  let g:tabnr = tabpagenr()
  tabnew
  call g:buffer.open()
  call g:buffer.switch(v:false, g:tabnr)
Then:
  AssertEqual g:tabnr, tabpagenr()
  AssertEqual g:buffer.bufnr(), bufnr('%')

Execute (Buffer: Cleanup):
  let g:tabnr = tabpagenr('$')
  while g:tabnr ># 2
    execute 'tabnext ' . g:tabnr
    if bufname('%') ==# ''
      tabclose
    endif
    let g:tabnr -= 1
  endwhile
  call g:buffer.destroy()
